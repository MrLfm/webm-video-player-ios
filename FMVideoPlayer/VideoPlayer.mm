//
//  VideoPlayer.m
//

#import "VideoPlayer.h"
#import "VideoTool.h"
#import <SDL2/SDL.h>
#include <vector>
#include <deque>

#ifdef __cplusplus
extern "C" {
#endif
#include <ffmpeg/libavcodec/avcodec.h>
#include <ffmpeg/libavformat/avformat.h>
#include <ffmpeg/libswresample/swresample.h>
#ifdef __cplusplus
}
#endif

@interface VideoPlayer ()
{
    AVFormatContext *fmtCtx;
    AVCodecContext *videoCodecCtx;
    AVCodecContext *audioCodecCtx;
    int videoStreamIndex;
    int audioStreamIndex;
    
    SwrContext *swrCtx;
    
    dispatch_queue_t decodeQueue;
    BOOL stopRequested;
    
    // éŸ³é¢‘ç¼“å­˜
    std::vector<int16_t> audio_buffer;
    std::mutex buffer_mutex;
    
    // æ·»åŠ æš‚åœæ§åˆ¶
    dispatch_semaphore_t pauseSemaphore;
}

@property (nonatomic, strong, readwrite) AVSampleBufferDisplayLayer *displayLayer;
@property (nonatomic, assign, readwrite) BOOL isPlaying;
@property (nonatomic, assign, readwrite) BOOL isPaused;
@property (nonatomic, copy)   NSString *currentURL;
@property (nonatomic, assign) BOOL isLoopPlay;
@property (nonatomic, assign) BOOL hasCache;
@end

@implementation VideoPlayer

- (instancetype)init {
    if (self = [super init]) {
        //        NSLog(@"â„¹ï¸ åˆå§‹åŒ–è§†é¢‘æ’­æ”¾å™¨");
        _displayLayer = [[AVSampleBufferDisplayLayer alloc] init];
        _displayLayer.videoGravity = AVLayerVideoGravityResizeAspectFill;// å…¨å±æ’­æ”¾
        decodeQueue = dispatch_queue_create("video.decode.queue", DISPATCH_QUEUE_SERIAL);
        self.isPlaying = NO;
        stopRequested = NO;
        self.isPaused = NO;
        pauseSemaphore = dispatch_semaphore_create(0);
    }
    return self;
}

- (void)playWithURL:(NSString *)urlString repeats:(BOOL)repeats {
    self.isLoopPlay = repeats;
    self.currentURL = urlString;
    [self playWithURL:urlString];
}

- (void)playWithURL:(NSString *)urlString {
    if (self.isPlaying) {
        NSLog(@"âš ï¸ æ’­æ”¾å¤±è´¥ï¼ç¦æ­¢é‡å¤æ’­æ”¾");
        return;
    }
    
    if (!_displayLayer) {
        NSLog(@"âŒ æ˜¾ç¤ºå±‚æœªåˆå§‹åŒ–ï¼Œæ— æ³•æ’­æ”¾è§†é¢‘");
        return;
    }
    stopRequested = NO;
    
    // æ’­æ”¾å‰æ¸…é™¤æ—§æ•°æ®
    [self clearCacheWithCompletion:^{
        __weak typeof(self) weakSelf = self;
        dispatch_async(decodeQueue, ^{
            __strong typeof(weakSelf) strongSelf = weakSelf;
            if (!strongSelf) {
                return;
            }
            
            NSLog(@"â„¹ï¸ å¼€å§‹æ’­æ”¾è§†é¢‘ï¼š%@", urlString);
            [weakSelf decodeLoopWithURL:urlString];
        });
    }];
}

#pragma mark - æš‚åœå’Œæ¢å¤æ§åˆ¶

- (void)pause {
    if (!self.isPlaying) {
        NSLog(@"âš ï¸ æš‚åœå¤±è´¥ï¼å½“å‰æ²¡æœ‰åœ¨æ’­æ”¾è§†é¢‘");
        return;
    }
    
    if (self.isPaused) {
        NSLog(@"âš ï¸ è§†é¢‘å·²ç»å¤„äºæš‚åœçŠ¶æ€ï¼Œæ— éœ€æš‚åœ");
        return;
    }
    
    NSLog(@"â¸ï¸ æš‚åœè§†é¢‘æ’­æ”¾");
    self.isPaused = YES;
    
    // æš‚åœéŸ³é¢‘
    SDL_PauseAudio(1);
    
    // æš‚åœè§†é¢‘æ˜¾ç¤ºå±‚
    if (_displayLayer) {
        [_displayLayer.sampleBufferRenderer stopRequestingMediaData];
    }
}

- (void)resume {
    if (!self.isPlaying) {
        NSLog(@"âš ï¸ æ¢å¤æ’­æ”¾å¤±è´¥ï¼æ²¡æœ‰åœ¨æ’­æ”¾è§†é¢‘");
        return;
    }
    
    if (!self.isPaused) {
        NSLog(@"âš ï¸ è§†é¢‘æ²¡æœ‰æš‚åœï¼Œæ— éœ€æ¢å¤");
        return;
    }
    
    NSLog(@"â–¶ï¸ æ¢å¤è§†é¢‘æ’­æ”¾");
    self.isPaused = NO;
    
    // æ¢å¤éŸ³é¢‘
    SDL_PauseAudio(0);
    
    // æ¢å¤è§†é¢‘æ˜¾ç¤ºå±‚
    if (_displayLayer) {
        [_displayLayer.sampleBufferRenderer requestMediaDataWhenReadyOnQueue:dispatch_get_main_queue() usingBlock:^{}];
    }
    
    // å‘é€ä¿¡å·ï¼Œå”¤é†’è¢«æš‚åœçš„è§£ç çº¿ç¨‹
    dispatch_semaphore_signal(pauseSemaphore);
}

// åˆ‡æ¢æš‚åœ/æ’­æ”¾çŠ¶æ€
- (void)togglePause {
    if (self.isPaused) {
        [self resume];
    }
    else {
        [self pause];
    }
}

// æ³¨æ„ğŸ“¢è¿™é‡Œåªå¤„ç†ä¸è€—æ—¶çš„æ“ä½œï¼
- (void)stopWithCompletion:(void (^)())completion {
    if (!self.isPlaying) {
        if (completion) {
            completion();
        }
        NSLog(@"âš ï¸ åœæ­¢æ’­æ”¾å¤±è´¥ï¼æ²¡æœ‰åœ¨æ’­æ”¾è§†é¢‘");
        return;
    }
    
    NSLog(@"â„¹ï¸ æ­£åœ¨åœæ­¢éŸ³é¢‘å’Œè§†é¢‘æ’­æ”¾...");
    
    // å¦‚æœå½“å‰æ˜¯æš‚åœçŠ¶æ€ï¼Œå…ˆæ¢å¤ä»¥ä¾¿æ­£å¸¸é€€å‡º
    if (self.isPaused) {
        self.isPaused = NO;
        dispatch_semaphore_signal(pauseSemaphore);
    }
    
    SDL_PauseAudio(1);
    stopRequested = YES;
    self.isPlaying = NO;
    
    NSLog(@"âœ… å·²åœæ­¢éŸ³é¢‘å’Œè§†é¢‘æ’­æ”¾");
    
    if (completion) {
        completion();
    }
}

- (void)clearCacheWithCompletion:(void (^)())completion {
    if (self.isPlaying) {
        if (completion) {
            completion();
        }
        NSLog(@"âš ï¸ æ¸…é™¤ç¼“å­˜å¤±è´¥ï¼è¯·å…ˆåœæ­¢æ’­æ”¾è§†é¢‘");
        return;
    }
    
    if (!self.hasCache) {
        if (completion) {
            completion();
        }
        return;
    }
    NSLog(@"â„¹ï¸ æ­£åœ¨æ¸…ç†æ’­æ”¾å™¨ç¼“å­˜...");
    
    // 1ã€é‡Šæ”¾è§£ç å™¨ï¼Œæ¸…é™¤ç¼“å­˜
    if (videoCodecCtx) {
        avcodec_free_context(&videoCodecCtx);
        videoCodecCtx = NULL;
    }
    if (audioCodecCtx) {
        avcodec_free_context(&audioCodecCtx);
        audioCodecCtx = NULL;
    }
    if (fmtCtx) {
        avformat_close_input(&fmtCtx);
        fmtCtx = NULL;
    }
    if (swrCtx) {
        swr_free(&swrCtx);
        swrCtx = NULL;
    }
    
    [[VideoTool sharedInstance] clear];
    
    // 2ã€æ¸…ç©ºéŸ³é¢‘ç¼“å†²åŒº
    {
        std::lock_guard<std::mutex> lock(buffer_mutex);
        audio_buffer.clear();
    }
    
    if (!_displayLayer) {
        self.hasCache = NO;
        if (completion) {
            completion();
        }
        return;
    }
    
    // 3ã€æ¸…ç©ºè§†é¢‘ç¼“å†²åŒº
    // åœæ­¢æ¥æ”¶æ–°æ•°æ®
    [_displayLayer.sampleBufferRenderer stopRequestingMediaData];
    dispatch_async(dispatch_get_main_queue(), ^{
        // æ¸…é™¤å·²æ˜¾ç¤ºçš„å†…å®¹ï¼Œå¿…é¡»åœ¨ä¸»çº¿ç¨‹æ‰§è¡Œ
        [self.displayLayer.sampleBufferRenderer flushWithRemovalOfDisplayedImage:true completionHandler:^{
            NSLog(@"âœ… å·²æ¸…ç†æ’­æ”¾å™¨ç¼“å­˜");
            
            self.hasCache = NO;
            if (completion) {
                completion();
            }
        }];
    });
}

#pragma mark - Decode Loop
- (void)decodeLoopWithURL:(NSString *)urlString {
    if (!self) return;
    if (self.isPlaying) {
        //        NSLog(@"âš ï¸ decodeLoop å·²ç»åœ¨æ‰§è¡Œï¼Œç¦æ­¢é‡å¤è¿›å…¥");
        return;
    }
    self.isPlaying = YES;
    self.isPaused = NO; // é‡ç½®æš‚åœçŠ¶æ€
    BOOL playbackCompleted = NO;
    
    @autoreleasepool {
        if (!fmtCtx) {
            fmtCtx = avformat_alloc_context();
        }
        
        self.hasCache = YES;// äº§ç”Ÿç¼“å­˜
        
        if (avformat_open_input(&fmtCtx, urlString.UTF8String, NULL, NULL) != 0) {
            NSLog(@"âŒ æ‰“å¼€è§†é¢‘æ–‡ä»¶å¤±è´¥ï¼Œæ— æ³•æ’­æ”¾è§†é¢‘");
            self.isPlaying = NO;
            return;
        }
        if (avformat_find_stream_info(fmtCtx, NULL) < 0) {
            NSLog(@"âŒ æŸ¥æ‰¾è§†é¢‘æµä¿¡æ¯å¤±è´¥ï¼Œæ— æ³•æ’­æ”¾è§†é¢‘");
            self.isPlaying = NO;
            return;
        }
        
        // å¸§ç´¢å¼•
        audioStreamIndex = av_find_best_stream(fmtCtx, AVMEDIA_TYPE_AUDIO, -1, -1, NULL, 0);
        videoStreamIndex = av_find_best_stream(fmtCtx, AVMEDIA_TYPE_VIDEO, -1, -1, NULL, 0);
        
        for (unsigned i = 0; i < fmtCtx->nb_streams; i++) {
            if (fmtCtx->streams[i]->codecpar->codec_type == AVMEDIA_TYPE_VIDEO && videoStreamIndex < 0) {
                videoStreamIndex = i;
            }
            else if (fmtCtx->streams[i]->codecpar->codec_type == AVMEDIA_TYPE_AUDIO && audioStreamIndex < 0) {
                audioStreamIndex = i;
            }
        }
        if (videoStreamIndex < 0) {
            NSLog(@"âŒ æœªæ‰¾åˆ°è§†é¢‘æµï¼Œæ— æ³•æ’­æ”¾è§†é¢‘");
            self.isPlaying = NO;
            return;
        }
        
        // è§†é¢‘è§£ç å™¨
        AVCodecParameters *videoCodecPar = fmtCtx->streams[videoStreamIndex]->codecpar;
        const AVCodec *videoCodec = avcodec_find_decoder(videoCodecPar->codec_id);
        videoCodecCtx = avcodec_alloc_context3(videoCodec);
        avcodec_parameters_to_context(videoCodecCtx, videoCodecPar);
        avcodec_open2(videoCodecCtx, videoCodec, NULL);
        
        // éŸ³é¢‘è§£ç å™¨
        if (audioStreamIndex >= 0) {
            AVCodecParameters *audioCodecPar = fmtCtx->streams[audioStreamIndex]->codecpar;
            const AVCodec *audioCodec = avcodec_find_decoder(audioCodecPar->codec_id);
            audioCodecCtx = avcodec_alloc_context3(audioCodec);
            avcodec_parameters_to_context(audioCodecCtx, audioCodecPar);
            avcodec_open2(audioCodecCtx, audioCodec, NULL);
            
            // åˆå§‹åŒ– SDL éŸ³é¢‘
            [self initAudioPlayer];
        }
        
        // è·å–è§†é¢‘å¸§ç‡
        AVStream *videoStream = fmtCtx->streams[videoStreamIndex];
        AVRational fps = av_guess_frame_rate(fmtCtx, videoStream, NULL);
        double frameRate = (double)fps.num / fps.den;
        if (frameRate <= 0) {
            frameRate = 30.0; // é»˜è®¤å¸§ç‡
        }
        double frameDuration = 1.0 / frameRate;
        //    NSLog(@"ğŸ¥ è§†é¢‘å¸§ç‡: %.2f fps, å¸§é—´éš”: %.3fç§’", frameRate, frameDuration);
        [[VideoTool sharedInstance] setFPS:frameRate];// è®¾ç½®å¸§ç‡
        
        // Decode Loop
        AVPacket *pkt = av_packet_alloc();
        AVFrame *videoFrame = av_frame_alloc();
        AVFrame *audioFrame = av_frame_alloc();
        
        CFAbsoluteTime startTime = CFAbsoluteTimeGetCurrent();
        CFAbsoluteTime pauseStartTime = 0;
        CFAbsoluteTime totalPauseTime = 0;
        int frameIndex = 0;
        
        while (!stopRequested && av_read_frame(fmtCtx, pkt) >= 0) {
            if (stopRequested) break;// æ¯æ¬¡å‘é€æ–°å¸§å‰æ£€æŸ¥ stopRequestedï¼Œé˜²æ­¢åœ¨è°ƒç”¨stopæ–¹æ³•åä»ç„¶enqueueè€å¸§
            
            // å¤„ç†æš‚åœé€»è¾‘
            if (self.isPaused) {
                pauseStartTime = CFAbsoluteTimeGetCurrent();
                NSLog(@"ğŸ”„ è§£ç çº¿ç¨‹è¿›å…¥æš‚åœçŠ¶æ€");
                
                // ç­‰å¾…æ¢å¤ä¿¡å·
                dispatch_semaphore_wait(pauseSemaphore, DISPATCH_TIME_FOREVER);
                
                if (stopRequested) {
                    break;
                }
                
                // è®¡ç®—æš‚åœæ—¶é—´ï¼Œè°ƒæ•´æ’­æ”¾æ—¶é—´åŸºå‡†
                CFAbsoluteTime pauseEndTime = CFAbsoluteTimeGetCurrent();
                totalPauseTime += (pauseEndTime - pauseStartTime);
                NSLog(@"ğŸ”„ è§£ç çº¿ç¨‹æ¢å¤è¿è¡Œ");
            }
            
            // è§£ç è§†é¢‘æ•°æ®
            if (pkt->stream_index == videoStreamIndex) {
                avcodec_send_packet(videoCodecCtx, pkt);
                
                while (!stopRequested && !self.isPaused && avcodec_receive_frame(videoCodecCtx, videoFrame) == 0) {
                    // è°ƒæ•´æ—¶é—´è®¡ç®—ï¼Œè€ƒè™‘æš‚åœæ—¶é—´
                    CFAbsoluteTime expectedTime = startTime + frameIndex * frameDuration + totalPauseTime;
                    CFAbsoluteTime currentTime = CFAbsoluteTimeGetCurrent();
                    if (currentTime < expectedTime) {
                        usleep((expectedTime - currentTime) * 1000000);
                    }
                    
                    // è½¬ CMSampleBuffer
                    CMSampleBufferRef sbuf = [[VideoTool sharedInstance] getRenderDataWithFrame:videoFrame];
                    if (sbuf) {
                        dispatch_async(dispatch_get_main_queue(), ^{
                            if (!stopRequested && !self.isPaused && self.displayLayer) {
                                [self.displayLayer enqueueSampleBuffer:sbuf];
                            }
                            CFRelease(sbuf);
                        });
                    }
                    frameIndex++;
                    av_frame_unref(videoFrame);
                }
            }
            
            // è§£ç éŸ³é¢‘æ•°æ®
            else if (pkt->stream_index == audioStreamIndex) {
                avcodec_send_packet(audioCodecCtx, pkt);
                while (!stopRequested && !self.isPaused && avcodec_receive_frame(audioCodecCtx, audioFrame) == 0) {
                    // è½¬ PCM 16bit
                    int out_channels = audioCodecCtx->channels;
                    int out_sample_rate = audioCodecCtx->sample_rate;
                    
                    // åˆå§‹åŒ– SwrContext
                    if (!swrCtx) {
                        swrCtx = swr_alloc_set_opts(NULL,
                                                    av_get_default_channel_layout(out_channels),
                                                    AV_SAMPLE_FMT_S16,
                                                    out_sample_rate,
                                                    av_get_default_channel_layout(audioCodecCtx->channels),
                                                    audioCodecCtx->sample_fmt,
                                                    audioCodecCtx->sample_rate,
                                                    0, NULL);
                        swr_init(swrCtx);
                    }
                    
                    int nb_samples = audioFrame->nb_samples;
                    int16_t *outBuffer = (int16_t *)malloc(nb_samples * out_channels * sizeof(int16_t));
                    uint8_t *outPtr[1] = {(uint8_t *)outBuffer};
                    swr_convert(swrCtx, outPtr, nb_samples, (const uint8_t **)audioFrame->data, nb_samples);
                    
                    // æ”¾å…¥ SDL ç¼“å†²åŒº
                    {
                        std::lock_guard<std::mutex> lock(buffer_mutex);
                        audio_buffer.insert(audio_buffer.end(), outBuffer, outBuffer + nb_samples * out_channels);
                    }
                    free(outBuffer);
                    av_frame_unref(audioFrame);
                }
            }
            av_packet_unref(pkt);
        }
        
        av_frame_free(&videoFrame);
        av_frame_free(&audioFrame);
        av_packet_free(&pkt);
        
        // æ£€æŸ¥æ˜¯æ­£å¸¸æ’­æ”¾ç»“æŸè¿˜æ˜¯è¢«åœæ­¢
        if (!stopRequested) {
            playbackCompleted = YES;
        }
    }
    
    self.isPlaying = NO;
    self.isPaused = NO;
    
    if (playbackCompleted && self.isLoopPlay) {
        NSLog(@"â„¹ï¸ é‡æ–°æ’­æ”¾è§†é¢‘");
        [self playWithURL:self.currentURL];
    }
}

#pragma mark - SDL éŸ³é¢‘
- (void)initAudioPlayer {
    SDL_SetMainReady();
    
    // åˆå§‹åŒ–éŸ³é¢‘è§£ç å™¨
    if (SDL_WasInit(SDL_INIT_AUDIO) == 0) {
        if (SDL_Init(SDL_INIT_AUDIO) < 0) {
            NSLog(@"âŒ éŸ³é¢‘è§£ç å™¨åˆå§‹åŒ–å¤±è´¥ï¼Œæ— æ³•æ’­æ”¾éŸ³é¢‘ï¼š%s", SDL_GetError());
            return;
        }
    }
    
    // å…ˆå…³é—­éŸ³é¢‘ï¼Œå†ç”¨æ–°å‚æ•°åˆ›å»ºéŸ³é¢‘ã€‚å¦‚æœä¸å…³é—­ï¼Œå¯èƒ½ä¼šå¯¼è‡´éŸ³é¢‘è§£æé”™è¯¯ï¼ˆéŸ³é¢‘åƒæ˜¯0.9å€é€Ÿæ’­æ”¾ï¼‰
    if (SDL_GetAudioStatus() != SDL_AUDIO_STOPPED) {
//        NSLog(@"âš ï¸ éŸ³é¢‘æœªåœæ­¢ï¼Œå°è¯•å…³é—­");
        SDL_CloseAudio();
    }
    
    SDL_AudioSpec spec;
    spec.freq = audioCodecCtx->sample_rate;
    spec.format = AUDIO_S16SYS;
    spec.channels = audioCodecCtx->channels;
    spec.samples = 1024;
    spec.callback = sdl_audio_callback;
    spec.userdata = (__bridge void *)self;// ä»£ç†
    
    if (SDL_OpenAudio(&spec, NULL) < 0) {
        NSLog(@"âŒ å¼€å¯éŸ³é¢‘å¤±è´¥ï¼Œæ— æ³•æ’­æ”¾éŸ³é¢‘ï¼š%s", SDL_GetError());
        return;
    }
//    NSLog(@"SDL éŸ³é¢‘å·²å¼€å¯ï¼Œé‡‡æ ·ç‡ï¼š%dï¼Œé€šé“æ•°ï¼š%d", spec.freq, spec.channels);
    SDL_PauseAudio(0);
}

void sdl_audio_callback(void *userdata, Uint8 *stream, int len) {
    VideoPlayer *player = (__bridge VideoPlayer *)userdata;
    std::lock_guard<std::mutex> lock(player->buffer_mutex);
    
    if (player->audio_buffer.empty()) {
        SDL_memset(stream, 0, len);
        return;
    }
    
    int copy_size = len / 2;
    int available_size = player->audio_buffer.size();
    if (copy_size > available_size) copy_size = available_size;
    
    SDL_memcpy(stream, player->audio_buffer.data(), copy_size * 2);
    player->audio_buffer.erase(player->audio_buffer.begin(), player->audio_buffer.begin() + copy_size);
}

- (void)dealloc {
    NSLog(@"FMVideoPlayer dealloc");
    [self stopWithCompletion:nil];
}

@end
